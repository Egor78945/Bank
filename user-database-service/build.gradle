plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'nu.studer.jooq' version '9.0'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2024.0.0")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'net.devh:grpc-server-spring-boot-starter:3.1.0.RELEASE'
    implementation 'com.google.protobuf:protobuf-java:3.24.3'
    implementation 'com.google.guava:guava:32.0.1-jre'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    runtimeOnly 'org.postgresql:postgresql'

    jooqGenerator 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.24.3"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.69.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

import org.jooq.meta.jaxb.Logging

jooq {
    version = '3.19.15'
    edition = nu.studer.gradle.jooq.JooqEdition.OSS

    configurations {
        main {
            generateSchemaSourceOnCompilation = true

            generationTool {
                logging = Logging.DEBUG
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = "jdbc:postgresql://${System.getenv('POSTGRES_USER_HOST')}:${System.getenv('POSTGRES_USER_PORT')}/${System.getenv('POSTGRES_USER_DATABASE_NAME')}"
                    user = "${System.getenv('POSTGRES_USER_USERNAME')}"
                    password = "${System.getenv('POSTGRES_USER_PASSWORD')}"
                    properties {
                        property {
                            key = 'ssl'
                            value = 'false'
                        }
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        forcedTypes {
                            forcedType {
                                name = 'varchar'
                                includeExpression = '.*'
                                includeTypes = 'JSONB?'
                            }
                            forcedType {
                                name = 'varchar'
                                includeExpression = '.*'
                                includeTypes = 'INET'
                            }
                        }
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'nu.studer.sample'
                        directory = 'build/generated-src/jooq/main'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

tasks.register('update', JavaExec) {
    mainClass = "liquibase.integration.commandline.Main"
    classpath = sourceSets.main.runtimeClasspath
    args = [
            "--changeLogFile=src/main/resources/db/changelog/main/main-changelog.yml",
            "--url=jdbc:postgresql://${System.getenv('POSTGRES_USER_HOST')}:${System.getenv('POSTGRES_USER_PORT')}/${System.getenv('POSTGRES_USER_DATABASE_NAME')}",
            "--username=${System.getenv('POSTGRES_USER_USERNAME')}",
            "--password=${System.getenv('POSTGRES_USER_PASSWORD')}",
            "update"
    ]
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('generateJooq').configure {
    allInputsDeclared = false
    launcher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

